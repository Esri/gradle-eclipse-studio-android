// Configuration of actual build script
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }

}

// List of plugins to apply
apply plugin: 'android'
apply plugin: 'eclipse'
apply plugin: 'maven'

// Build configurations
configurations {
    compile
}

// Repositories to use for application wide
// dependency resolution
repositories {
    jcenter()
    maven {
      url 'https://esri.bintray.com/arcgis'
    }
}

dependencies {
    // local binary dependency
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.esri.arcgis.android:arcgis-android:10.2.7'
}

// Android plugin settings
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"


     sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

    }

    // Global configuration that apply to
    // all build types
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    packagingOptions{
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

}

// Including configurations into Eclipse
// eclipse.classpath.plusConfigurations += configurations.compile

// Configuring Eclipse classpath
eclipse.classpath.file {

    // Manipulation of the classpath XML before merging local
    // and generated copies
    beforeMerged { classpath ->
        // Remove all source entries to avoid overlap
        classpath.entries.removeAll() { c ->
            c.kind == 'src'
        }
    }


    // Direct manipulation of the generated classpath XML
    withXml {

        // Since non-default source directories are used
        // they need to be manually added to the classpath XML
        def node = it.asNode()

        // Main source directory
        node.appendNode('classpathentry kind="src" path="src/main/java"')

        // Generated code directory
        node.appendNode('classpathentry kind="src" path="gen"')

        // Filter out dependencies unwanted for Eclipse
        node.children().removeAll() { c ->
            def path = c.attribute('path')
            path != null
        }

    }

}



// Configuring Eclipse project
eclipse.project {
    // Project name that will appear in your project explorer
    name = 'ArcGISAndroid'

    // Letting Eclipse know that the project is Android
    natures 'com.android.ide.eclipse.adt.AndroidNature'
    buildCommand 'com.android.ide.eclipse.adt.ResourceManagerBuilder'
    buildCommand 'com.android.ide.eclipse.adt.PreCompilerBuilder'
    buildCommand 'com.android.ide.eclipse.adt.ApkBuilder'
}
